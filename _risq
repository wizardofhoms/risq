#compdef risq
#autoload

# Entrypoint ================================================================================== 

_risq() {
    local cmd
    if (( CURRENT > 2 )); then
        cmd=${words[2]}
        # Set the context for the subcommand
        curcontext="${curcontext%:*:*}:risq-$cmd"
        # Narrow the range of words we are looking at to exclude `pass`
        (( CURRENT-- ))
        shift words
        # Run the completion for the subcommand
        case "${cmd}" in
            (help)      _help ;;
            pass)       _normal -p $service ;;
            verify)     _verify ;;
        esac
    else
        local ret=1
        local line state
        _arguments -C \
            "${_flags_base[@]}" \
            '(- : *)'--version'[Print risq version and exit]' \
            "1: :_risq_commands" \
            && ret=0
    fi
}

# Subcomands ==================================================================================

# Completes subcommands of the risq CLI.
_risq_commands ()
{
    local -a subcmds
    subcmds=(
        'help:Show help about a command'
        'pass:Use pass commands'
        'verify:Verify an onion link signature'
        'refresh:Fetch some lists and indexes of root trust keys, links to avoid, etc'
    )

    _describe -t commands 'commands' subcmds
}

# Completion dispatcher for help command.
_help ()
{
    local ret=1
    local line state
    _arguments -C \
        "1: :_risq_commands" && ret=0
}

# Completion dispatcher for verify command
_verify ()
{
    local ret=1
    local line state

    _arguments -C \
        "${_flags_verify[@]}" \
        "1:onion v3 URL:" \
        && ret=0
}

# Flags =======================================================================================

# Flags that are present on all commands
_flags_base=(
    '(-v --verbose)'{-v,--verbose}'[Enable verbose logging]'
    '(- : *)'{-h,--help}'[Print help for the command]'
    )

_flags_verify=(
    '(-k --key)'{-k,--key}'[Optional public key file to import in keyring, to check the link against]:GPG public key file:_path_files'
    '(-r --refresh)'{-r,--refresh}'[Run the risq refresh command to update blacklisted onion files and root keys]'
    )


# don't run the completion function when being source-ed or eval-ed
if [ "$funcstack[1]" = "_risq" ]; then
    _risq
fi
